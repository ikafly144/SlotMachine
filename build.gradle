plugins {
    id 'java'
    id("io.github.goooler.shadow") version "8.1.8"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'net.sabafly'
version = '0.1.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
    compileOnly 'dev.cerus.maps:common:3.8.10'
    compileOnly 'dev.cerus.maps:plugin:3.8.10'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly 'org.geysermc.floodgate:api:2.2.3-SNAPSHOT'
    implementation 'me.lucko:commodore:2.2'
    implementation "co.aikar:taskchain-bukkit:3.7.2"
    implementation "org.spongepowered:configurate-yaml:4.2.0-SNAPSHOT"
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.3.0'
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName.set("SlotMachine.jar")

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    minimize()
    relocate 'me.lucko.commodore', 'net.sabafly.slotmachine.commodore'
    relocate 'co.aikar.taskchain', 'net.sabafly.slotmachine.taskchain'
    relocate 'com.fasterxml.uuid', 'net.sabafly.slotmachine.uuid'
}

tasks {
    runServer {
        downloadPlugins {
            github("cerus", "maps", "3.8.7", "maps-plugin.jar")
            github("MilkBowl", "Vault", "1.7.3", "Vault.jar")
            github("Jikoo", "OpenInv", "5.1.1", "OpenInv.jar")
            modrinth("emeraldbank", "0.3.0")
        }
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.1")
    }
}
